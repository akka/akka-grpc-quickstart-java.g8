buildscript {
  repositories {
    mavenLocal()
    gradlePluginPortal()
    // TODO remove when Akka gRPC 1.0.0 is final
    maven {
      url "https://dl.bintray.com/akka/snapshots/"
    }
  }

  dependencies {
    // see https://plugins.gradle.org/plugin/com.lightbend.akka.grpc.gradle
    // for the currently latest version.
    //classpath 'com.lightbend.akka.grpc:akka-grpc-gradle-plugin:$akka_grpc_version$'
    // TODO remove when Akka gRPC 1.0.0 is final
    classpath 'com.lightbend.akka.grpc:akka-grpc-gradle-plugin:1.0.0-M1+37-732f2dee'
  }
}

plugins {
  id 'java'
  id 'application'
}

apply plugin: 'com.lightbend.akka.grpc.gradle'

repositories {
  mavenCentral()
  mavenLocal()
  // TODO remove when Akka gRPC 1.0.0 is final
  maven {
    url "https://dl.bintray.com/akka/snapshots/"
  }
}

akkaGrpc {
  language = "Java"
  generateClient = true
  generateServer = true
}

// Define a separate configuration for managing the dependency on Jetty ALPN agent.
configurations {
  alpnagent
}

dependencies {
  implementation 'com.typesafe.akka:akka-actor-typed_$scala_major_version$:$akka_version$'
  implementation 'com.typesafe.akka:akka-stream_$scala_major_version$:$akka_version$'
  implementation 'com.typesafe.akka:akka-discovery_$scala_major_version$:$akka_version$'
  implementation 'com.typesafe.akka:akka-protobuf_$scala_major_version$:$akka_version$'

  testImplementation 'junit:junit:4.13'
  testImplementation 'com.typesafe.akka:akka-actor-testkit-typed_$scala_major_version$:$akka_version$'
  testImplementation 'com.typesafe.akka:akka-stream-testkit_$scala_major_version$:$akka_version$'

  // Configuration for modules that use Jetty ALPN agent
  alpnagent 'org.mortbay.jetty.alpn:jetty-alpn-agent:$jetty_alpn_agent_version$'
}

test {
  jvmArgs "-javaagent:" + configurations.alpnagent.asPath
}

task runServer(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath

  main = 'com.example.helloworld.GreeterServer'

  jvmArgs "-javaagent:" + configurations.alpnagent.asPath
}


task runClient(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath

  main = 'com.example.helloworld.GreeterClient'

  // arguments to pass to the application
  if (project.hasProperty("GreeterClient.user"))
    args  project.getProperty("GreeterClient.user")
}

application {
  mainClassName = 'com.example.helloworld.GreeterServer'
}
